#!/usr/bin/env osascript -l AppleScript

-- imessage
-- Send messages (iMessages by default) from the command line
--
-- Usage:
-- imessage <contact address> <message> [service type]

on run arguments
	if count of arguments is not less than 2 then

		set buddyAddress to item 1 of arguments
		set messageText to item 2 of arguments

		if count of arguments is 3 then
			set serviceType to item 3 of arguments
		else
			set serviceType to "iMessage"
		end if

		tell application "Messages"
			set messagingService to missing value
			set serviceList to get every service
			repeat with currentService in every service
				if service type of currentService as string is serviceType then
					set messagingService to currentService
				end if
			end repeat
			
			if messagingService is not missing value then
				send messageText to buddy buddyAddress of messagingService
				return "Message Sent using " & service type of messagingService as string & " service \"" & name of messagingService & "\""
			else
				return "Error: Could not find a service whose type was called \"" & serviceType & "\""
			end if
		end tell

	else
		return "Error: You need to pass both an address or phone number, and a message"
	end if
end run
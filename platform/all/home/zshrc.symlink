# ZSH Configuration
# Geoff Stokes <http://github.com/geoffstokes/dotfiles>

# This file bootstraps the rest of the configuration.
# Loading mechanism borrowed from Zach Holman's dotfiles <https://github.com/holman>

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/platform/(all|$UNAME|all-but-^$UNAME)/zsh/*.zsh)

# NOTE: path files are now loaded by .zshenv

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

# Enable help (recommended by homebrew. TODO: Ask why)
unalias run-help >/dev/null 2>&1
autoload run-help
HELPDIR=/usr/local/share/zsh/helpfiles

unset config_files

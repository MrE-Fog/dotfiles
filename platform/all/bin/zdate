#!/usr/bin/env zsh

# zdate
# zsh-powered strftime(3) date formatting with preset formats.
# Uses BSD-style strftime(3) formatting strings (yes, even on GNU systems)

zmodload zsh/datetime

# Preset Date Formats
case $1 in
	beats|b)
		format="@%i"
		;;
	short|s)
		format="%L:%M %P"
		;;
	medium|m)
		format="%a %L:%M %P"
		;;
	long|l)
		format="%a %b %f %L:%M %P"
		;;
	full|f)
		format="%A, %B %f %G %L:%M:%S %P"
		;;
	8601|iso|i)
		format="%Y-%m-%dT%H:%M:%S%z"
		;;
	*)
		format=$1
		;;
esac

# Detect Meridiem Format String and override
meridiem=$(strftime "%P" "$EPOCHSECONDS")
meridiem=${${meridiem/P/}/A/}

if [[ -z "$meridiem" ]]; then
	# GNU strftime(3)
	format=${format/"%P"/"%p"}
fi

timestamp=$EPOCHSECONDS

if [[ $# -gt 1 ]]; then
	timestamp=$2
fi

# Swatch Internet Time (Beats)

function beats() {

	zmodload zsh/mathfunc
	integer BEATS

	((BEATS=floor(($(strftime "%S" "$1")+($(strftime "%M" "$1")*60)+(($(strftime "%H" "$1")+1)*3600))/86.4)))

	printf $BEATS

}

if [[ $format = *%i* ]]; then
	format=${format/"%i"/"$(TZ=UTC beats $timestamp)"}
fi

strftime "$format" "$timestamp"

#!/usr/bin/env osascript

-- imessage
-- Send iMessages from the command line
--
-- Usage:
-- imessage <contact address> [<contact address>...] <message>

on run arguments
	if count of arguments is not less than 2 then
		set buddyAddressList to items 1 thru -2 of arguments
		set messageText to last item of arguments

		tell application "Messages"
			set messagingService to missing value
			set messagingService to first service whose service type is iMessage
			
			if messagingService is not missing value then
				set buddyList to {}
				
				repeat with buddyAddress in buddyAddressList
					copy (buddy buddyAddress of messagingService) to the end of buddyList
				end repeat

				send messageText to make new text chat with properties {participants: buddyList}
				return "Message Sent using " & service type of messagingService as string & " service \"" & name of messagingService & "\""
			else
				return "Error: Could not find a configured iMessage service"
			end if
		end tell

	else
		return "Error: You need to pass both an address or phone number, and a message"
	end if
end run